buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: "maven"
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.13-3.0.00"
group = "betterwithmods"
archivesBaseName = "BetterWithMods"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

if (System.getenv().BUILD_NUMBER != null) {//adds the build number to the end of the version string if on a build server
    version += "-${System.getenv().BUILD_NUMBER}"
}

minecraft {
    version = "1.12.2-14.23.5.2770"
    runDir = "run"
    mappings = "stable_39"
    replace "%VERSION%": "${project.version}"
    useDepAts = true
}

repositories {
    maven { url "http://maven.blamejared.com" }
    maven { url 'http://dvs1.progwml6.com/files/maven' }
    maven { url "http://maven.amadornes.com/" }
    maven { url "http://www.ryanliptak.com/maven/" }
    maven { url "http://maven.tterrag.com" }
    maven { url "https://maven.thiakil.com/" }
    mavenCentral()
}
dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:+"
    deobfCompile "applecore:AppleCore:1.12.2-3.1.0"
    deobfCompile "team.chisel.ctm:CTM:MC1.12+"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"

    if (!project.hasProperty("singleproject")) {
        deobfCompile "betterwithmods:BetterWithLib:1.13-2.0.00"
    } else {
        compile project(':BetterWithLib')
    }

    compile 'org.easytesting:fest-assert:1.4'
    compile 'org.easytesting:fest-util:1.1.6'

}

sourceSets {
    main {
        java {
            exclude '**/Thaumcraft.java'
        }
        resources {
            exclude 'assets/betterwithmods/docs/en_us/assets/**'
        }
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'bwm_at.cfg'
    }
    exclude 'assets/betterwithmods/docs/en_us/assets/**'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier "dev"
}

artifacts {
    archives deobfJar
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
